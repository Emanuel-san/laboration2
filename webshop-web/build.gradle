plugins {
    id "org.openapi.generator" version "6.6.0"
    id "org.springframework.boot" version "3.1.2"
}

group = 'lab2.webshop'
version = '0.0.1-SNAPSHOT'

java {
    sourceCompatibility = '17'
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

dependencies {
    implementation project(':webshop-api-lib')
    implementation 'org.springframework.boot:spring-boot-starter-oauth2-client'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.projectlombok:lombok'
    implementation 'org.openapitools:jackson-databind-nullable'
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui'
    implementation 'org.springframework.data:spring-data-mongodb'
    implementation 'io.swagger.core.v3:swagger-annotations:'
    annotationProcessor 'org.projectlombok:lombok'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

compileJava.dependsOn tasks.named("openApiGenerate")

openApiGenerate {
    generatorName.set("java")
    inputSpec.set("${project(':webshop-api-lib').projectDir}/src/main/resources/webshop.yml")
    outputDir.set("$buildDir/generated")
    apiPackage.set("lab2.webshop.openapi.api")
    modelPackage.set("lab2.webshop.openapi.model")
    library.set("resttemplate")
    configOptions.set([
            'generateClientAsBean'     : 'true',
            'useJakartaEe'     : 'true'
    ])
}
sourceSets {
    main {
        java {
            srcDir("$buildDir/generated/src/main/java")
        }
    }
}

tasks.named('test') {
    useJUnitPlatform()
}

